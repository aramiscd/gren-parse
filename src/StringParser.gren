module StringParser exposing
    ( Parser
    , Result
    , string
    , fail
    , succeed
    , skip
    , optional
    , zeroOrMore
    , oneOrMore
    , oneOf
    , sequence
    , map
    , andThen
    , run
    )


{-| Parsing string input

---
---

## Data types

---

@docs Parser

---

@docs Result

---
---

## Basic parsers

---

@docs string

---

@docs fail

---

@docs succeed

---
---

## Combinators

---

@docs skip

---

@docs optional

---

@docs zeroOrMore

---

@docs oneOrMore

---

@docs oneOf

---

@docs sequence

---
---

## Transformations

---

@docs map

---

@docs andThen

---
---

## Execution

---

@docs run

-}


{-| A parser processes an input string.
If it is successful, it produces a result.
Otherwise, it produces `Nothing`.
-}
type alias Parser a =
    String -> Maybe ( Result a )


{-| If a parser has only processed part of its input, a `backlog` remains.
This is the unprocessed part of the input.
A `Result a` consists of this `backlog` and the sequence of produced `values` of type `a`.
-}
type alias Result a =
    { backlog : String      -- Input not yet read
    , values : Array a      -- intermediate results
    }


{-| Parse a fixed string.
```
> parse = Parse.string "abc"
> parse "abcdef"
Just { backlog = "def", values = [ "abc" ] }
```
-}
string : String -> Parser String
string match input =
    if String.startsWith match input
    then
        Just
            { backlog = String.dropFirst ( String.count match ) input
            , values = [ match ]
            }
    else
        Nothing


{-| Always fail.
```
> parse = Parse.fail
> parse "abc"
Nothing
```
-}
fail : Parser a
fail input = Nothing


{-| Always succeed and produce a fixed list of values.
```
> parse = Parse.succeed [ "x" ]
> parse "abc"
Just ( backlog = "abc", values = [ "x" ] )
```
-}
succeed : Array a -> Parser a
succeed values input =
    Just
        { backlog = input
        , values = values
        }


{-| Apply a parser and discard the result.
```
> parse = Parse.skip ( Parse.string "abc" )
> parse "abcdef"
Just { backlog = "def", values = [] }
```
-}
skip : Parser a -> Parser a
skip parse input =
    when parse input is
        Just result -> Just { result | values = [] }
        Nothing -> Nothing


{-| Apply a parser if possible.
```
> parse = Parse.optional ( Parse.string "x" )
> parse "xyz"
Just { backlog = "yz", values = [ "x" ] }
```
Do not fail if the parser cannot be applied.
```
> parse = Parse.optional ( Parse.string "x" )
> parse "abc"
Just { backlog = "abc", values = [] }
```
-}
optional : Parser a -> Parser a
optional parse input =
    when parse input is
        Just result -> Just result
        Nothing -> Just { backlog = input, values = [] }


{-| Apply a parser as often as possible.
```
> parse = Parse.zeroOrMore ( Parse.string "x" )
> parse "xxxxa"
Just { backlog = "a", values = [ "x", "x", "x", "x" ] }
```
```
> parse = Parse.zerorMore ( Parse.string "x" )
> parse "aaaaa"
Just { backlog = "aaaaa", values = [] }
```
-}
zeroOrMore : Parser a -> Parser a
zeroOrMore parse input =
    optional ( oneOrMore parse ) input


{-| Apply a parser as often as possible, but at least once.
```
> parse = Parse.oneOrMore ( Parse.string "x" )
> parse "xxxxa"
Just { backlog = "a", values = [ "x", "x", "x", "x" ] }
```
```
> parse = Parse.oneOrMore ( Parse.string "x" )
> parse "aaaaa"
Nothing
```
-}
oneOrMore : Parser a -> Parser a
oneOrMore parse input =
    sequence [ parse, optional ( oneOrMore parse ) ] input


{-| Apply the first successful parser from a list of parsers.
```
> parse =
      Parse.oneOf
          [ Parse.string "x"
          , Parse.string "y"
          , Parse.string "z"
          ]
> parse "yak"
Just { backlog = "ak", values = [ "y" ] }
```
-}
oneOf : Array ( Parser a ) -> Parser a
oneOf parsers input =
    oneOfHelper parsers { backlog = input, values = [] }


oneOfHelper : Array ( Parser a ) -> Result a -> Maybe ( Result a )
oneOfHelper parsers acc =
    when Array.popFirst parsers is
        Nothing -> Nothing
        Just { first, rest } ->
            when first acc.backlog is
                Nothing -> oneOfHelper rest acc
                Just result -> Just result


{-| Apply several parsers in succession.
```
> parse =
      Parse.sequence
          [ Parse.string "x"
          , Parse.string "y"
          , Parse.string "z"
          ]
> parse "xyz"
Just { backlog = "", values = [ "x", "y", "z" ] }
```
-}
sequence : Array ( Parser a ) -> Parser a
sequence parsers input =
    sequenceHelper parsers { backlog = input, values = [] }


sequenceHelper : Array ( Parser a ) -> Result a -> Maybe ( Result a )
sequenceHelper parsers acc =
    when Array.popFirst parsers is
        Nothing -> Just acc
        Just { first, rest } ->
            when first acc.backlog is
                Nothing -> Nothing
                Just { backlog, values } ->
                    sequenceHelper rest { backlog = backlog, values = acc.values ++ values }


{-| Transform the parsed values.
```
> parse = Parse.map ( Array.map String.reverse ) ( string "abc" )
> parse "abcdef"
Just { backlog = "def", values = [ "cba" ] }
```
This can be used, for example, to change the target data type of a parser.
```
> parse = Parse.string "true" |> Parse.map ( \ _ -> [ True ] )
> parse "true"
> Just { backlog = "", values = [ True ] }
```
-}
map : ( Array a -> Array b ) -> Parser a -> Parser b
map f parse input =
    when parse input is
        Nothing -> Nothing
        Just result -> Just { backlog = result.backlog, values = f result.values }


{-| Transform the result of a parser.

This can do everything that `map` can do.
In addition, you can use it to decide whether a parser should fail based on the values it produces.

For example, we can use it to limit `zeroOrMore` to three repetitions.
```
> parse =
      Parse.zeroOrMore ( Parse.string "x" )
      |> Parse.andThen
          ( \ result ->
              if Array.length result.values > 3 then Nothing
              else Just result
          )
```
```
> parse "xxx"
Just { backlog = "", values = [ "x", "x", "x" ] }
```
```
> parse "xxxx"
Nothing
```
-}
andThen : ( Result a -> Maybe ( Result b ) ) -> Parser a -> Parser b
andThen f parse input =
    when parse input is
        Nothing -> Nothing
        Just result -> f result


{-| Apply a parser and produce a final result.

`run` expects a parser to process its input completely (i.e. to leave an empty backlog),
and to produce exactly one value.
If successful, the produced value is returned.  Otherwise, nothing is returned.

```
> parser = Parse.string "foo"
```
```
> run parser "foo"
Just "foo"
```
```
> run parser "foox"
Nothing
```
-}
run : Parser a -> String -> Maybe a
run parse input =
    when parse input is
        Just { backlog = "", values = [ value ] } -> Just value
        _ -> Nothing
